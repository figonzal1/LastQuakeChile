apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

android {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    // Initialize a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()
    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        lastquakechilesign {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "cl.figonzal.lastquakechile"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 31 //realease en 29
        versionName "1.5.4-beta"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        if (rootProject.file("local.properties").exists()) {
            properties.load(rootProject.file("local.properties").newDataInputStream())
        }

        // Inject the Maps API key into the manifest
        manifestPlaceholders = [mapsApiKey: properties.getProperty("MAPS_API_KEY", "")]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.lastquakechilesign
            FirebasePerformance {
                instrumentationEnabled true
            }
        }
        debug {
            FirebasePerformance {
                instrumentationEnabled false
            }
            firebaseCrashlytics {
                // If you don't need crash reporting for your debug build,
                // you can speed up your build by disabling mapping file uploading.
                mappingFileUploadEnabled false
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.10"

    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0' //Corrutinas
    implementation 'androidx.preference:preference-ktx:1.2.0' //preference
    implementation 'com.google.android.material:material:1.5.0' //materia design
    implementation 'androidx.core:core-splashscreen:1.0.0-beta01' //splash screen

    implementation 'com.jakewharton.timber:timber:5.0.1' //Timber logs
    implementation 'com.google.code.gson:gson:2.8.9' //GSON

    //Life cycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"

    //Google Play Core
    implementation 'com.google.android.play:core-ktx:1.8.1'

    //Google Maps
    // KTX for the Maps SDK for Android
    implementation 'com.google.maps.android:maps-ktx:3.2.1'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    //ROOM
    // Room components
    implementation "androidx.room:room-ktx:2.4.0"
    kapt "androidx.room:room-compiler:2.4.0"
    androidTestImplementation "androidx.room:room-testing:2.4.0"

    //Retrofit
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation 'com.github.bumptech.glide:glide:4.12.0' //Carga de imagenes

    //Firebase BOM
    implementation platform('com.google.firebase:firebase-bom:26.7.0')
    //Firebase Dependency
    implementation 'com.google.firebase:firebase-ads'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-dynamic-links'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-perf'

    //junit
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    //UI AUTOMATOR
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    //Espreso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")
}
